// KQL samples - Authentication using web service key/basic authentication

// successful authentication (raw data)
traces
| where timestamp > ago(60d) // adjust as needed
| where customDimensions.eventId == "RT0020" 
| extend aadTenantId = customDimensions.aadTenantId
, eventId = customDimensions.eventId 
, environmentName = customDimensions.environmentName
, environmentType = customDimensions.environmentType
// note that in versions 17.0-17.2, category is always 'API' (bug was fixed in 17.3)
, category = customDimensions.category
, endpoint = customDimensions.endpoint
, authenticationStatus = customDimensions.authenticationStatus
, authenticationType = customDimensions.authenticationType
, usertelemetryId = case(
  // user telemetry id was introduced in the platform in version 20.0
  toint( substring(customDimensions.componentVersion,0,2)) >= 20, user_Id
, 'N/A'
)

// failed authentication (raw data)
traces
| where timestamp > ago(60d) // adjust as needed
| where customDimensions.eventId == "RT0021" 
| extend aadTenantId = customDimensions.aadTenantId
, eventId = customDimensions.eventId 
, environmentName = customDimensions.environmentName
, environmentType = customDimensions.environmentType
// note that in versions 17.0-17.2, category is always 'API' (bug was fixed in 17.3)
, category = customDimensions.category
, endpoint = customDimensions.endpoint
, authenticationStatus = customDimensions.authenticationStatus
, authenticationType = customDimensions.authenticationType
, usertelemetryId = case(
  // user telemetry id was introduced in the platform in version 20.0
  toint( substring(customDimensions.componentVersion,0,2)) >= 20, user_Id
, 'N/A'
)


// successful/failed authentication calls joined to web service telemetry to extract endpoint, http headers, ...
// use this to find who is calling using basic authentication
let auth_calls =
traces
| where customDimensions.eventId in ( "RT0020" , "RT0021" )
| where operation_Id != 'N/A'
| project timestamp, operation_Id
;
let ws_calls = 
traces
| where customDimensions.eventId == 'RT0008'
| project operation_Id
, httpHeaders = customDimensions.httpHeaders
, aadTenantId = customDimensions.aadTenantId
, environmentName = customDimensions.environmentName
, environmentType = customDimensions.environmentType
, companyName = customDimensions.companyName
, alObjectId = customDimensions.alObjectId
, alObjectName = customDimensions.alObjectName
, alObjectType = customDimensions.alObjectType
, category = customDimensions.category
, endpoint = customDimensions.endpoint
, httpStatusCode = customDimensions.httpStatusCode 
| extend httpHeadersTmp =  tostring( httpHeaders)
| extend httpHeadersJSON = parse_json(httpHeadersTmp)
;
auth_calls
| join kind=leftouter ws_calls on $left.operation_Id == $right.operation_Id
| where isnotempty( aadTenantId )