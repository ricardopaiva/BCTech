// KQL samples - Email

// Email sent successfully (raw data)
traces
| where 1==1
    and timestamp > ago(60d) // adjust as needed
    and customDimensions.eventId == 'AL0000CTV'
| project timestamp
, aadTenantId = customDimensions.aadTenantId
, environmentName = customDimensions.environmentName
, environmentType = customDimensions.environmentType
, alEmailMessageID = customDimensions.alEmailMessageID
, alConnector = customDimensions.alConnector
, usertelemetryId = case(
  // user telemetry id was introduced in the platform in version 20.0
  toint( substring(customDimensions.componentVersion,0,2)) >= 20, user_Id
, 'N/A'
)

// Failed to send email (raw data)
traces
| where 1==1
    and timestamp > ago(60d) // adjust as needed
    and customDimensions.eventId == 'AL0000CTP'
| project timestamp
, aadTenantId = customDimensions.aadTenantId
, environmentName = customDimensions.environmentName
, environmentType = customDimensions.environmentType
, alEmailMessageID = customDimensions.alEmailMessageID
, alConnector = customDimensions.alConnector
// alErrorCallStack and alErrorText available from version 19.0
, alErrorCallStack = customDimensions.alErrorCallStack
, alErrorText = customDimensions.alErrorText
, usertelemetryId = case(
  // user telemetry id was introduced in the platform in version 20.0
  toint( substring(customDimensions.componentVersion,0,2)) >= 20, user_Id
, 'N/A'
)